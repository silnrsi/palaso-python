c Burmese Keyboard for Unicode Encoding. 
c Derived from an original by MJP Hosken (SIL)
c 
c This Layout has been designed to resemble that used by WinMyanmar Systems 
c in their non-Unicode fonts. However, it is much simplified because of the 
c advantages of Unicode technology. Some keys have changed as a result.
c 
c Copyright (C) 2004,2008 Keith Stribley, www.thanlwinsoft.org
c 			  Martin Hosken, SIL International
c Bugs/Comments: <mailto:devel@thanlwinsoft.org>
c Distributable under the terms of the GNU Lesser General Public License,
c either version 2.1 of the License, or (at your option) any later version.
c http://www.gnu.org/licenses/old-licenses/lgpl-2.1.html
c 
c KRS   1.00    28-May-2004     Initial version
c KRS   1.01    31-May-2004     Added 25CC to consU   
c KRS   1.02    01-Jul-2004     Improved behaviour when deleting 1031
c                               Added option to type 1031 + stack key for Kinzi
c                               Shift+Space = ZNSP and ~ = WJ
c KRS   1.03    04-Sep-2004     Now uses U+200D with YRWH medials following U+1004
c MJPH  1.1     21-APR-2006     Convert to proposed new Unicode 5.1 model n3043
c KRS   2.1     22-Jul-2006     Correct handling of u102A when types as composite
c KRS	2.1.1	12-Dec-2007	    Change references to Unicode 5.1
c                               Change filler to 200C, since 200B causes problems in OOo
c KRS   2.2     23-Apr-2008     Add extra rules to correct 0478 when used as consonants
c                               Additional rules to correct common typos
c                               Change hot key to Ctrl+Alt+M for Windows
c KRS   2.2.1   21-May-2008     Add full syllable context for medial substitutions
c KRS   2.2.2                   Fix medial regressions
c KRS   2.2.3   19-Jul-2008     More corrections for when numbers are used
c                               and out of sequence upper/lower dot
c KRS   2.2.4   30-Jul-2008     Fixed 078 cons cons bug
c MJPH  0.1     17-Sep-2008     Create kyu keyboard
c MJPH  0.2     31-AUG-2009     Enforce good ordering for kyu

NAME "kyu-Mymr 0.2"
VERSION 5.0
c BITMAP myWin.bmp
HOTKEY  "^+M"

begin Unicode > use(Main)
c qwertyuiop[ is same as Win
c asdfghjkl; is same as Win
c zxcvbm is same as Win      
c currently _ is mapped to * but it is multiply in Win
c , is U101A in both Win and here. ',' is < here    
c in Win 'P' is U100F, here at 'E' - Win's E is not needed anymore
c W is now U101D - the symbol in Win at this position is not used
c and includes U101D anyway
c Duplicates/macros that aren't strictly necessary EKL:F$| 
c The medials are also can be created using stacking: GjsS
c Pali characters that might need a location: U1050-1059
c Some of the characters <.}]^ could be changed to make room
c Should U101B, U1002 be moved somewhere more accessible e.g. R,U rather than &,*?
store(baseK)    'qwertyui' \
                "op['zxc"  \
                'vbn,./' \
                'QWERT' \
                'IOP{A"' \
                'ZXCVBN?' \
                '-=!@#%^&' \
                '*()_+' \
                '\]}<>`~'
                
store(baseU)    U+1006 U+1010 U+1014 U+1019 U+1021 U+1015 U+1000 U+1004 \
                U+101E U+1005 U+101F U+1012 U+1016 U+1011 U+1001 \
                U+101C U+1018 U+100A U+101A U+002E U+104B \
                U+1029 U+101D U+103F U+1023 U+1024 \
                U+1034 U+1025 U+100F U+1027 U+1017 U+1013 \
                U+1007 U+100C U+1003 U+1020 U+1026 U+1008 U+104A \
                U+002D U+003D U+100D U+100E U+A92E U+0025 U+002F U+101B \
                U+1002 U+0028 U+0029 U+005F U+002B \
                U+104F U+2018 U+2019 U+002C U+104E U+25CC U+2060     
                
store(numK)     '1234567890' 
store(numU)     U+1041 U+1042 U+1043 U+1044 U+1045 U+1046 U+1047 U+1048 U+1049 \
                U+1040
c Some numbers can be used incorrectly as letters
store(numOrLetK) '078'
store(numOrLetU) U+1040 U+1047 U+1048
store(numAsLetU) U+101D U+101B U+1002

store(aftereK)  'mgh;M'
store(aftereU)  U+102C U+102B U+1037 U+1038 U+1064

c sub units of aftereK
store (aaK)     'mg'
store (aaU)     U+102C U+102B
store (lDotK)   'h'
store (lDotU)   U+1037
store (visargaK) ';'
store (visargaU) U+1038
                      
store(uDiaK)    'dDfYU'
store(uDiaU)    U+102D U+102E U+1072 U+1073 U+1074

store(anuK)     'JH'
store(anuU)     U+1032 U+1036
                  
store(lDiaK)    'klKL'
store(lDiaU)    U+102F U+1030 U+102F U+1030


store(currencyK)    '$'       

c 25cc is added here to allow it to be used to show stand alone diacritics
store(consU)    U+1000 U+1001 U+1002 U+1003 U+1004 U+1005 U+1006 U+1007 \
                U+1008 U+1009 U+100A U+100B U+100C U+100D U+100E U+100F \
                U+1010 U+1011 U+1012 U+1013 U+1014 U+1015 U+1016 U+1017 \
                U+1018 U+1019 U+101A U+101B U+101C U+101D U+101E U+101F \
                U+1020 U+1021 
store(consK)    'uc*CipqZ' \
                  'NUn#X!@P' \
                  "wx'" \
                  '"eyzAbr,&vWo[' \
                  'Vt'

store(sDiaK)     'sjSG'
store(sDiaU)     U+103B U+103C U+103E U+103D
store(diaU)      outs(sDiaU) outs(uDiaU) outs(lDiaU) U+1031 outs(anuU)
    
c split up diacritics - user shouldn't need to know canonical order 
c Y/R should be first and can't come together     
          
store(yDiaU)    U+103B
store(rDiaU)    U+103C
store(wDiaU)    U+103D 
store(hDiaU)    U+103E
store(yrDiaU)   U+103B U+103C 
store(yrwDiaU)   U+103B U+103C U+103D

store(yDiaK) 's'
store(rDiaK) 'j'
store(hDiaK) 'S'
store(wDiaK) 'G'
store(yrDiaK) outs(yDiaK) outs(rDiaK)

store(m1DiaK)   's'
store(m2DiaK)   'sj'
c store(m3DiaK)   'sj_'
store(m3DiaK)   'sjG'
store(m4DiaK)   'sjGS'
store(mDia)     U+103B U+103C U+103D U+103E

c this is a weired stacked combination of 100B,100C                      
store(ttatthaK) '|' 

c U+200B causes problems in OpenOffice, but Padauk now removes 200C/D,
c so it doesn't have correct effect
store(filler)   U+200B

c F is Kinzi - it should be possible to create a Kinzi   
store(kinziK)   'F'
store(kinziU)   U+1004    

store(vowelEK)  'a'
store(vowelEU)  U+1031  

c create some classes for diacritics that might have been 
c sensibly typed before the user types the medial
store(allDiaU)  U+1031 U+102F U+1030 U+102D U+1032 U+1036 U+1037 U+102E U+1072 U+1073 U+1074
c only a subset can correctly occur together  
c (U+1031 U+1037) (U+102D U+102F) 
c (U+102F U+1036) (U+1032 U+1037) (U+102D U+1036)
store(firstDiaU) U+1031 U+102D U+102F U+1032 
store(secondDiaU) U+102F U+1036 U+1037    
c It is possible for U+102F U+102D U+1037 to occur,
c but in this case its fairly obvious to type the medials
c before the final U+1037    

store(stackK)   '`'
store(stackU)   U+1039


group(Main) using keys

c Deal with vowel E before consonant 
+ any(vowelEK) > outs(filler) outs(vowelEU)
c stacker key
U+1025 any(consU) + any(stackK) > U+1009 outs(stackU) index(consU, 2)
U+1025 any(numOrLetU) + any(stackK) > U+1009 outs(stackU) index(numOrLetU, 2)
any(consU) + any(stackK) > outs(stackU) index(consU, 1)
any(numOrLetU) + any(stackK) > outs(stackU) index(numAsLetU, 1)
any(numOrLetU) any(consU) + any(stackK) > index(numAsLetU, 1) outs(stackU) index(consU, 2)
any(numOrLetU) any(numOrLetU) + any(stackK) > index(numAsLetU, 1) outs(stackU) index(numAsLetU, 2)
c rearrange vowel E to behind consonant in stacking situation
c U+1031 U+101E U+101E + any(stackK) > U+103F U+1031
U+1031 any(consU) + any(stackK) > outs(stackU) index(consU, 2) U+1031
U+1005 U+1031 + any(yDiaK) > U+1008 U+1031

c modify space keys
 + [SHIFT K_SPACE] > U+200B      
 
c Convert sequences for composite vowels into single code point
U+101E U+103C U+1031 U+102c + any(aftereK) > U+102A
U+101E + 'j' > U+1029
U+1025 + 'D' > U+1026          

 + any(numK) > index(numU, 1)
 + any(baseK) > index(baseU, 1)
 + any(aftereK) > index(aftereU, 1)
any(filler) U+1031 + any(numOrLetK) > index(numAsLetU, 3) U+1031
any(filler) U+1031 + any(baseK) > index(baseU, 3) U+1031
c any(filler) U+1031 + any(baseK) > U+1031 U+25CC index(baseU, 3)

c these allow the user to type upper and lower diacritics in
c any order, but to still get the storage right
any(uDiaU) U+1037 + any(lDiaK) > index(uDiaU, 1) index(lDiaU, 3) U+1037
any(lDiaU) U+1037 + any(uDiaK) > index(uDiaU, 3) context
U+1037 + any(uDiaK) > index(uDiaU, 2) context
U+1037 + any(lDiaK) > index(lDiaU, 2) context

any(anuU) + any(uDiaK) > index(uDiaU, 2) context
any(lDiaU) any(anuU) + any(uDiaK) > index(uDiaU, 3) context
any(anuU) + any(lDiaK) > index(lDiaU, 2) context
c it is unlikely that other combinations will occur with U1037 
c and aa so this rules are probably OK
U+1037 + any(aaK) > index(aaU, 2) context
c allow tone marks to be typed in either order
U+1038 + any(lDotK) > index(lDotU, 2) context
U+1037 + any(anuK) > index(anuU, 2) U+1037

c any(uDiaU) + any(lDiaK) > index(lDiaU, 2) context
any(lDiaU) + any(uDiaK) > index(uDiaU, 2) context
any(numOrLetU) + any(lDiaK) > index(numAsLetU, 1) index(lDiaU, 2)
any(numOrLetU) + any(uDiaK) > index(numAsLetU, 1) index(uDiaU, 2)
 + any(lDiaK) > index(lDiaU, 1)
 + any(uDiaK) > index(uDiaU, 1)

 + any(ttatthaK) > U+100B U+1039 U+100C 
 + any(currencyK) > U+1000 U+103B U+1015 U+103A
 + ':' > U+102B U+103A
 
c now deal with the medials, need to arrange order appropriately
c most complicated is when all 3 medials are together but out of order 

any(rDiaU) any(wDiaU) any(hDiaU) any(uDiaU) any(lDiaU) any(anuU) + any(m1DiaK) > index(mDia, 7) context
any(rDiaU) any(wDiaU) any(hDiaU) any(uDiaU) any(lDiaU) + any(m1DiaK) > index(mDia, 6) context
any(rDiaU) any(wDiaU) any(hDiaU) any(uDiaU) + any(m1DiaK) > index(mDia, 5) context
any(rDiaU) any(wDiaU) any(hDiaU) + any(m1DiaK) > index(mDia, 4) context
any(rDiaU) any(wDiaU) + any(m1DiaK) > index(mDia, 3) context
any(rDiaU) + any(m1DiaK) > index(mDia, 2) context
any(wDiaU) any(hDiaU) any(uDiaU) any(lDiaU) any(anuU) + any(m2DiaK) > index(mDia, 6) context
any(wDiaU) any(hDiaU) any(uDiaU) any(lDiaU) + any(m2DiaK) > index(mDia, 5) context
any(wDiaU) any(hDiaU) any(uDiaU) + any(m2DiaK) > index(mDia, 4) context
any(wDiaU) any(hDiaU) + any(m2DiaK) > index(mDia, 3) context
any(wDiaU) + any(m2DiaK) > index(mDia, 2) context
any(hDiaU) any(uDiaU) any(lDiaU) any(anuU) + any(m3DiaK) > index(mDia, 5) context
any(hDiaU) any(uDiaU) any(lDiaU) + any(m3DiaK) > index(mDia, 4) context
any(hDiaU) any(uDiaU) + any(m3DiaK) > index(mDia, 3) context
any(hDiaU) + any(m3DiaK) > index(mDia, 2) context

any(uDiaU) any(lDiaU) any(anuU) + any(m4DiaK) > index(mDia, 4) context
any(lDiaU) any(anuU) + any(m4DiaK) > index(mDia, 3) context
any(anuU) + any(m4DiaK) > index(mDia, 2) context

any(lDiaU) any(anuU) + any(uDiaK) > index(uDiaU, 3) context
any(anuU) + any(uDiaK) > index(uDiaU, 2) context

any(anuU) + any(lDiaK) > index(lDiaU, 2) context
 + any(anuK) > index(anuU, 1)

U+1005 + 's' > U+1008
 + any(sDiaK) > index(sDiaU, 1)

+ any(kinziK) > index(kinziU, 2) U+103A U+1039
any(consU) + any(kinziK) > index(kinziU, 2) U+103A U+1039 index(consU, 1)
any(numOrLetU) + any(kinziK) > index(kinziU, 2) U+103A U+1039 index(numAsLetU, 1)
any(numU) + any(kinziK) > index(kinziU, 2) U+103A U+1039  index(numU, 1)
any(consU) U+1039 any(consU) + any(kinziK) > index(kinziU, 4) U+103A U+1039 context
any(consU) U+1039 any(consU) any(diaU) + any(kinziK) > index(kinziU, 5) U+103A U+1039 context
any(consU) U+1039 any(consU) any(diaU) any(diaU) + any(kinziK) > index(kinziU, 6) U+103A U+1039 context
any(consU) any(mDia) + any(kinziK) > index(kinziU, 3) U+103A U+1039 index(consU, 1) index(mDia, 2)
any(consU) any(diaU) + any(kinziK) > index(kinziU, 3) U+103A U+1039 index(consU, 1) index(diaU, 2)
any(consU) any(diaU) any(diaU) + any(kinziK) > index(kinziU, 4) U+103A U+1039 index(consU, 1) index(diaU, 2) index(diaU, 3)
any(consU) any(diaU) any(diaU) any(diaU) + any(kinziK) > index(kinziU, 5) U+103A U+1039 index(consU, 1) index(diaU, 2) index(diaU, 3) index(diaU, 4) 
any(consU) any(diaU) any(diaU) any(diaU) any(diaU) + any(kinziK) > index(kinziU, 6) U+103A U+1039 index(consU, 1) index(diaU, 2) index(diaU, 3) index(diaU, 4) index(diaU, 5) 

any(filler) U+1031 + [K_BKSP] > nul
any(baseU) U+1031 + [K_BKSP] > outs(filler) U+1031

U+1004 U+103A U+1039 any(consU) U+1031 + [K_BKSP] > U+1004 U+103A U+1039 outs(filler) U+1031
U+1039 any(consU) U+1031 + [K_BKSP] > U+1031
U+1039 any(consU) + [K_BKSP] > nul
